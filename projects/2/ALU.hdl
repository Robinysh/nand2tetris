// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
/**
 * ALU (Arithmetic Logic Unit):
 * Computes out = one of the following functions:
 *                0, 1, -1,
 *                x, y, !x, !y, -x, -y,
 *                x + 1, y + 1, x - 1, y - 1,
 *                x + y, x - y, y - x,
 *                x & y, x | y
 * on the 16-bit inputs x, y,
 * according to the input bits zx, nx, zy, ny, f, no.
 * In addition, computes the two output bits:
 * if (out == 0) zr = 1, else zr = 0
 * if (out < 0)  ng = 1, else ng = 0
 */
// Implementation: Manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) sets x = 0        // 16-bit constant
// if (nx == 1) sets x = !x       // bitwise not
// if (zy == 1) sets y = 0        // 16-bit constant
// if (ny == 1) sets y = !y       // bitwise not
// if (f == 1)  sets out = x + y  // integer 2's complement addition
// if (f == 0)  sets out = x & y  // bitwise and
// if (no == 1) sets out = !out   // bitwise not


CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute (out = x + y) or (out = x & y)?
        no; // negate the out output?
    OUT 
        out[16], // 16-bit output
        zr,      // if (out == 0) equals 1, else 0
        ng;      // if (out < 0)  equals 1, else 0

    PARTS:
        // x
        Mux16(a=x, b[0..15]=false, sel=zx, out=zxX);
        Not16(in=zxX, out=notzxX);
        Mux16(a=zxX, b=notzxX, sel=nx, out=nxX);

        // y
        Mux16(a=y, b[0..15]=false, sel=zy, out=zyY);
        Not16(in=zyY, out=notzyY);
        Mux16(a=zyY, b=notzyY, sel=ny, out=nyY);
        
        Add16(a=nxX, b=nyY, out=sumXY);
        And16(a=nxX, b=nyY, out=andXY);
        
        Mux16(a=andXY, b=sumXY, sel=f, out=fXY);
        
        Not16(in=fXY, out=notfXY);
        Mux16(a=fXY, b=notfXY, sel=no, out=outTmp);
        
        Or16(a=outTmp, b[0..15]=false, out=out);

        And16(a=outTmp, b=true, out[0..7]=outFirstHalf, out[8..15]=outSecondHalf);
        
        Or8Way(in=outFirstHalf, out=orStartHalf);
        Or8Way(in=outSecondHalf, out=orEndHalf);
        Or(a=orStartHalf, b=orEndHalf, out=anyOne);
        Not(in=anyOne, out=zr);

        And16(a=outTmp, b=true, out[15]=ng, out[0..14]=ignore);
}